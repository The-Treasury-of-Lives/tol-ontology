PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# Monastery Head Lineage ID
# Lineage Name -IGNORE (ignore)
# Person ID
# Sequence
# Ordinal Text
# start_date_century_id
# end_date_century_id
# start_date
# start_date_approx
# start_date_alternate
# end_date
# end_date_approx
# end_date_alternate

# Run:
# tarql -e utf-8 --dedup 50000 monastery_lineage_members.tq monastery-head-lineage-members-6-24-24.csv > tolMonasteryLineageMembers.ttl

CONSTRUCT {

    ?monastery_lineage_iri 
        gist:hasMember ?lineage_member_iri ;
        .
    
    ?lineage_member_iri
        a tolo:LineageMember ;
        gist:providesOrderFor ?person_iri ;
        gist:sequence ?sequence ;
        tolo:ordinal ?ordinal_int ;

        tolo:actualStartYear ?actual_start_datetime ;
        tolo:approximateStartYear ?approximate_start_datetime;
        tolo:alternateStartYear 
            ?alternate_start_datetime_1 ,
            ?alternate_start_datetime_2 ;
        tolo:occursDuring ?start_time_period_iri ;

        tolo:actualEndYear ?actual_end_datetime ;
        tolo:approximateEndYear ?approximate_end_datetime;
        tolo:alternateEndYear 
            ?alternate_end_datetime_1 ,
            ?alternate_end_datetime_2 ;
        tolo:occursDuring ?end_time_period_iri ;
        .

}

WHERE {

    # Testing
    # FILTER($ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_MonasteryLineage_", ?Monastery_Head_Lineage_ID)) AS ?monastery_lineage_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_LineageMember_", ?Monastery_Head_Lineage_ID, "_", ?Person_ID)) AS ?lineage_member_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?Person_ID)) AS ?person_iri)

    BIND(STRDT(?Sequence, xsd:integer) AS ?sequence) 
    BIND(STRDT(?Ordinal_Text, xsd:integer) AS ?ordinal_int) 


    # Dates

    # To create an xsd:dateTime from a year string
    BIND("-01-01T00:00:00" AS ?date_time_completion)

    # Start dates
    BIND(STRDT(CONCAT(?start_date, ?date_time_completion), xsd:dateTime) AS ?start_datetime)

    BIND(STRDT(CONCAT(?start_date_alternate, ?date_time_completion), xsd:dateTime) AS ?alternate_start_datetime_1)

    # Actual vs approximate vs alternate start year:

    # If no alternate and date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?start_date_alternate) && (!BOUND(?start_date_approx) || ?start_date_approx = "0"), ?start_datetime, ?unbound) AS ?actual_start_datetime)

    # If approximate, start date is the approximate date.
    # In some cases approximate = 1 co-occurs with start_date_century_id. The approximate value is ignored in this case, because it servers only to make actual years approximate. Century-based start is already approximate by definition.
    BIND(IF(?start_date_approx = "1", ?start_datetime, ?unbound) AS ?approximate_start_datetime)

    # If there's an alternate date, the start date is an alternate (neither has priority).
    BIND(IF(BOUND(?start_date_alternate), ?start_datetime, ?unbound) AS ?alternate_start_datetime_2)

    # Start century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?start_date_century_id)) AS ?start_time_period_iri) 

    # End dates
    BIND(STRDT(CONCAT(?end_date, ?date_time_completion), xsd:dateTime) AS ?end_datetime)

    BIND(STRDT(CONCAT(?end_date_alternate, ?date_time_completion), xsd:dateTime) AS ?alternate_end_datetime_1)

    # Actual vs approximate vs alternate end year:

    # If no alternate and date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?end_date_alternate) && (!BOUND(?end_date_approx) || ?end_date_approx = "0"), ?end_datetime, ?unbound) AS ?actual_end_datetime)

    # If approximate, end date is the approximate date.
    # In some cases approximate = 1 co-occurs with end_date_century_id. The approximate value is ignored in this case, because it servers only to make actual years approximate. Century-based end is already approximate by definition.
    BIND(IF(?end_date_approx = "1", ?end_datetime, ?unbound) AS ?approximate_end_datetime)

    # If there's an alternate date, the end date is an alternate (neither has priority).
    BIND(IF(BOUND(?end_date_alternate), ?end_datetime, ?unbound) AS ?alternate_end_datetime_2)

    # Start century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?end_date_century_id)) AS ?end_time_period_iri) 

}
