PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# Role ID
# Office Name (ignore)
# TOL P Member
# start_date_century_id (ignore - all blank)
# start_date
# start_date_approx
# start_date_alternate (ignore - all blank)
# end_date_century_id (ignore - all blank)
# end_date
# end_date_approx
# end_date_alternate (ignore - all blank)

# Run:
# tarql -e utf-8 --dedup 50000 vocations.tq role-members-8-4-24.csv > tolVocations.ttl

CONSTRUCT {

    ?vocation_iri 
        a tolo:Vocation ;
        skos:prefLabel ?vocation_label_en ;
        gist:hasParticipant ?person_iri ;
        gist:isCategorizedBy ?role_iri ; 
        tolo:actualStartYear ?actual_start_datetime ;
        tolo:approximateStartYear ?approximate_start_datetime ;
        tolo:actualEndYear ?actual_end_datetime ;
        tolo:approximateEndYear ?approximate_end_datetime ;
        tolo:occursDuring 
            ?start_date_century_id ,
            ?end_date_century_id ;
        tolo:test ?end_date ;
        .

}

WHERE {

    # Testing
    # FILTER($ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Vocation_", ?TOL_P_Member, "_", ?Role_ID)) AS ?vocation_iri)
    BIND(STRLANG(CONCAT("Person ", ?TOL_P_Member, " ", ?Office_Name, " Role"), "en") AS ?vocation_label_en)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Role_", LCASE(REPLACE(?Office_Name, " ", "_")))) AS ?role_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?TOL_P_Member)) AS ?person_iri)


    # Dates

    # To create an xsd:dateTime from a year string
    BIND("-01-01T00:00:00" AS ?date_time_completion)

    # Start date
    BIND(STRDT(CONCAT(?start_date, ?date_time_completion), xsd:dateTime) AS ?start_datetime)
    # Actual vs approximate start year:
    # If approximate, start date is the approximate date.
    BIND(IF(?start_date_approx = "1", ?start_datetime, ?unbound) AS ?approximate_start_datetime)
    # If date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?start_date_approx) || ?start_date_approx = "0", ?start_datetime, ?unbound) AS ?actual_start_datetime)

    # End date
    BIND(STRDT(CONCAT(?end_date, ?date_time_completion), xsd:dateTime) AS ?end_datetime)
    # Actual vs approximate end year:
    # If approximate, end date is the approximate date.
    BIND(IF(?end_date_approx = "1", ?end_datetime, ?unbound) AS ?approximate_end_datetime)
    # If date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?end_date_approx) || ?end_date_approx = "0", ?end_datetime, ?unbound) AS ?actual_end_datetime)

}
