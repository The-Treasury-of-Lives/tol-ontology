PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# Role ID
# Office Name (ignore)
# TOL P Member
# start_date_century_id 
# start_date
# start_date_approx
# start_date_alternate
# end_date_century_id 
# end_date
# end_date_approx
# end_date_alternate 

# Run:
# tarql -e utf-8 --dedup 50000 vocations.tq role-members-8-4-24.csv > tolVocations.ttl

CONSTRUCT {

    ?vocation_iri 
        a tolo:Vocation ;
        skos:prefLabel ?vocation_label_en ;
        gist:hasParticipant ?person_iri ;
        gist:isCategorizedBy ?role_iri ;

        tolo:actualStartYear ?actual_start_datetime ; 
        tolo:approximateStartYear ?approximate_start_datetime;
        tolo:alternateStartYear 
            ?alternate_datetime_1 ,
            ?alternate_datetime_2 ;

        tolo:actualEndYear ?actual_end_datetime ;
        tolo:approximateEndYear ?approximate_end_datetime;
        tolo:alternateEndYear 
            ?alternate_datetime_1 ,
            ?alternate_datetime_2 ;

        tolo:occursDuring 
            ?start_time_period_iri ,
            ?end_time_period_iri ;

        .

}

WHERE {

    # Testing
    # FILTER($ROWNUM = 279)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Vocation_", ?TOL_P_Member, "_", ?Role_ID)) AS ?vocation_iri)
    BIND(STRLANG(CONCAT("Person ", ?TOL_P_Member, " ", ?Office_Name, " Role"), "en") AS ?vocation_label_en)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Role_", LCASE(REPLACE(?Office_Name, " ", "_")))) AS ?role_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?TOL_P_Member)) AS ?person_iri)


    # Dates
    # To create an xsd:dateTime from a year string
    BIND("-01-01T00:00:00" AS ?date_time_completion)

    # Start dates
    BIND(
        COALESCE(
            IF(REGEX(?start_date, "^\\d{4}$"), ?start_date, ?unbound), 
            # Add initial 0 to 3-digit year to create a well-formed year
            IF(REGEX(?start_date, "^\\d{3}$"), CONCAT("0", ?start_date), ?unbound)
        ) 
        AS ?start_year
    )

    BIND(STRDT(CONCAT(?start_year, ?date_time_completion), xsd:dateTime) AS ?start_datetime)

    BIND(STRDT(CONCAT(?start_date_alternate, ?date_time_completion), xsd:dateTime) AS ?alternate_start_datetime_1)

    # Actual vs approximate vs alternate founding year:

    # If no alternate and date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?start_date_alternate) && (!BOUND(?start_date_approx) || ?start_date_approx = "0"), ?start_datetime, ?unbound) AS ?actual_start_datetime)

    # If approximate, start date is the approximate date.
    # In some cases approximate = 1 co-occurs with start_date_century_id. The approximate value is ignored in this case, because it servers only to make actual years approximate. Century-based start is already approximate by definition.
    BIND(IF(?start_date_approx = "1", ?start_datetime, ?unbound) AS ?approximate_start_datetime)

    # If there's an alternate date, the start date is an alternate (neither has priority).
    BIND(IF(BOUND(?start_date_alternate), ?start_datetime, ?unbound) AS ?alternate_start_datetime_2)

    # Start century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?start_date_century_id)) AS ?start_time_period_iri) 
    
    # End dates
    BIND(
        COALESCE(
            IF(REGEX(?end_date, "^\\d{4}$"), ?end_date, ?unbound), 
            # Add initial 0 to 3-digit year to create a well-formed year
            IF(REGEX(?end_date, "^\\d{3}$"), CONCAT("0", ?end_date), ?unbound)
        ) 
        AS ?end_year
    )

    BIND(STRDT(CONCAT(?end_year, ?date_time_completion), xsd:dateTime) AS ?end_datetime)

    BIND(STRDT(CONCAT(?end_date_alternate, ?date_time_completion), xsd:dateTime) AS ?alternate_end_datetime_1)

    # Actual vs approximate vs alternate end year:

    # If no alternate and date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?end_date_alternate) && (!BOUND(?end_date_approx) || ?end_date_approx = "0"), ?end_datetime, ?unbound) AS ?actual_end_datetime)

    # If approximate, end date is the approximate date.
    # In some cases approximate = 1 co-occurs with end_date_century_id. The approximate value is ignored in this case, because it servers only to make actual years approximate. Century-based end is already approximate by definition.
    BIND(IF(?end_date_approx = "1", ?end_datetime, ?unbound) AS ?approximate_end_datetime)

    # If there's an alternate date, the end date is an alternate (neither has priority).
    BIND(IF(BOUND(?end_date_alternate), ?end_datetime, ?unbound) AS ?alternate_end_datetime_2)

    # # End century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?end_date_century_id)) AS ?end_time_period_iri) 

}
