PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://ontologies.semanticarts.com/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tolo: <https://ontologies.treasuryoflives.org/tol/>
PREFIX tolx: <https://taxonomies.treasuryoflives.org/>
PREFIX told: <https://data.treasuryoflives.org/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>

## Columns
# incarnation_lineages_id (PK) -- ignore
# BDRC R ID -- ?bdrc__lineage_iri
# TOL R ID -- ?incarnation_lineage_iri
# Incarnation Name -- ?tol_name
# Tradition -- ?tradition_iri
# Wylie -- ?wylie_name
# Chinese -- ?chinese_name
# BDRC G ID -- monastery ID -- ignore (owl:sameAs will come from institution data)
# TOL G ID -- ?monastery_iri
# description -- ?html_description_iri

# Run: tarql --dedup 5000 incarnation_lineages.tq > incarnation_lineages.ttl

CONSTRUCT {

    <https://data.tol.org/d/incarnation_lineages>
        a owl:Ontology ;
        skos:definition "Defines incarnation lineages with associated monasteries and traditions."^^xsd:string ;
        skos:prefLabel "Incarnation Lineages"^^xsd:string ;
        skos:editorialNote "Auto-generated via TARQL." ;
	.

    ?incarnation_lineage_iri a tolo:IncarnationLineage ;
        owl:sameAs ?bdrc_lineage_iri ;
        gist:name ?tol_name , 
                  ?wylie_name , 
                  ?chinese_name ;
        gist:isConnectedTo ?monastery_iri ;       
        gist:isConnectedTo ?tradition_iri ;
        gist:isDescribedIn ?html_description_iri ;
        .

    ?html_description_iri a gist:FormattedContent ;
        gist:isExpressedIn media-txt:html ;
        gist:containedText ?description ;
        .

}

FROM <file:incarnation_lineages_22-12-06.csv>

WHERE {

    BIND(tarql:expandPrefixedName(CONCAT("told:_IncarnationLineage_", ?TOL_R_ID)) AS ?incarnation_lineage_iri)

    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_R_ID)) AS ?bdrc_lineage_iri) 

    # Names
    BIND(STRLANG(?Incarnation_Name, "en") AS ?tol_name)
    BIND(STRLANG(?Wylie, "bo-x-ewts") AS ?wylie_name)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?chinese_name)

    # Tradition
    OPTIONAL {
        ?tradition apf:strSplit(?Tradition ", ")
    }
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", REPLACE(?tradition, " ", "_"))) AS ?tradition_iri)

    # Monastery
    # owl:sameAs to ?BDRC_G_ID will come from monastery data, but will need IRI
    BIND(tarql:expandPrefixedName(CONCAT("told:_Monastery_", ?TOL_G_ID)) AS ?monastery_iri)


    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_Description_IncarnationLineage_", ?TOL_R_ID)) AS ?html_description_iri)

}
