PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 

## Columns
# incarnation_lineages_id (PK) -- ignore
# BDRC R ID -- ?bdrc__lineage_iri
# TOL R ID -- ?incarnation_lineage_iri
# Incarnation Name -- ?tol_name
# Tradition 1 -- ignore -- comes from tradition data
# Tradition ID 1 -- ?tradition_1_iri
# Tradition 2 -- ignore -- comes from tradition data
# Tradition ID 2 ?tradition_2_iri
# Wylie -- ?wylie_name
# Chinese -- ?chinese_name
# BDRC G ID -- monastery ID -- ignore (owl:sameAs will come from institution data)
# TOL G ID -- ?monastery_iri
# description -- ?html_description_iri

# Run: 
# tarql --dedup 5000 incarnation_lineages.tq incarnation-lineages-10-31-23.csv > tolIncarnationLineages.ttl

CONSTRUCT {

    <https://w3id.org/treasuryoflives/data/tolIncarnationLineages>
        a owl:Ontology ;
        skos:definition "Defines incarnation lineages with associated monasteries and traditions."^^xsd:string ;
        skos:prefLabel "Incarnation Lineages"^^xsd:string ;
        skos:editorialNote "Auto-generated via TARQL." ;
	    owl:imports <https://w3id.org/treasuryoflives/ontology/tolCoreX.x.x> ;
	    owl:imports <https://w3id.org/semanticarts/ontology/gistCore12.0.1> ;
	    owl:versionIRI <https://w3id.org/treasuryoflives/data/tolIncarnationLineagesX.x.x> ;
	.

    ?incarnation_lineage_iri a tolo:IncarnationLineage ;
        owl:sameAs ?bdrc_lineage_iri ;
        skos:prefLabel ?tol_name , 
                  ?wylie_name , 
                  ?chinese_name ;
        gist:isAffiliatedWith ?monastery_iri ;       
        gist:isAffiliatedWith ?tradition_1_iri ,
            ?tradition_2_iri ;
        .
       

    ?html_description_iri a gist:FormattedContent ;
        skos:prefLabel ?description_label ;
        gist:isExpressedIn media-txt:html ;
        gist:containedText ?description ;
        gist:isAbout ?incarnation_lineage_iri ;
        .
       
}

WHERE {

    # Testing
    FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_IncarnationLineage_", ?TOL_R_ID)) AS ?incarnation_lineage_iri)

    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_R_ID)) AS ?bdrc_lineage_iri) 

    # Names
    BIND(STRLANG(?Incarnation_Name, "en") AS ?tol_name)
    BIND(STRLANG(?Wylie, "bo-x-ewts") AS ?wylie_name)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?chinese_name)

    # Traditions
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", REPLACE(?Tradition_ID_1, "TOL", ""))) AS ?tradition_1_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", REPLACE(?Tradition_ID_2, "TOL", ""))) AS ?tradition_2_iri)

    # Monastery
    # owl:sameAs to ?BDRC_G_ID will come from monastery data, but will need IRI
    BIND(tarql:expandPrefixedName(CONCAT("told:_Monastery_", REPLACE(?TOL_G_ID, "TOL", ""))) AS ?monastery_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_Description_IncarnationLineage_", ?TOL_R_ID)) AS ?html_description_iri)

    BIND(STRLANG(CONCAT("The ", ?Incarnation_name, " Incarnation Lineage"), "en") AS ?description_label)

}
