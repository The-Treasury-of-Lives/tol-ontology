PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 

## Columns
# Incarnation Name -- ignore -- comes from incarnation lineage data
# Incarnation BDRC ID -- ignore -- comes from incarnation lineage data
# Incarnation TOL ID -- ?incarnation_lineage_iri
# Parent lineage TOL ID -- ?parent_lineage_iri
# Preceding incarnation member TOL ID -- ignore
# Incarnation sequence -- ?sequence
# Ordinal -- ?ordinal_int
# Person BDRC ID -- ignore -- comes from person data
# Person TOL ID -- ?person_iri
# Branched Lineage -- ignore
# New Seat Established -- ignore
# Notes -- ignore

# Run: 
# tarql -e utf-8 --dedup 5000 incarnation_members.tq incarnation-members-4-12-24.csv > tolIncarnationMembers.ttl

CONSTRUCT {

    ?incarnation_lineage_iri 
        tolo:isDerivedFrom ?parent_lineage_iri ;
        gist:hasMember ?incarnation_iri ;
        #tolo:hasPreincarnation ?preincarnation_iri ;
        .
    
    ?incarnation_iri 
        a tolo:Incarnation ;
        gist:providesOrderFor ?person_iri ;
        gist:sequence ?sequence ;
        tolo:ordinal ?ordinal_int ;
        .

}

WHERE {

    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_IncarnationLineage_", ?Incarnation_TOL_ID)) AS ?incarnation_lineage_iri)
    
    # Parent incarnation lineage
    BIND(tarql:expandPrefixedName(CONCAT("told:_IncarnationLineage_", ?Parent_lineage_TOL_ID)) AS ?parent_lineage_iri)

    # Incarnation
    BIND(tarql:expandPrefixedName(CONCAT("told:_Incarnation_", ?Incarnation_TOL_ID, "_", ?Person_TOL_ID)) AS ?incarnation_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?Person_TOL_ID)) AS ?person_iri)

    BIND(STRDT(?Ordinal, xsd:integer) AS ?ordinal_int) 
    BIND(STRDT(?Incarnation_Sequence, xsd:integer) AS ?sequence) 

    # Create incarnation except for preincarnations
    # BIND(IF(?Ordinal > 0, tarql:expandPrefixedName(CONCAT("told:_Incarnation_", ?Incarnation_TOL_ID, "_", ?Person_TOL_ID)), ?unbound) AS ?incarnation_iri) ;
    # BIND(IF(?Ordinal > 0, xsd:integer(?Ordinal), ?unbound) AS ?ordinal)

    # Preceding incarnation
    # BIND(IF(BOUND(Preceding_incarnation_member_TOL_ID), tarql:expandPrefixedName(CONCAT("told:_Incarnation_", ?Incarnation_TOL_ID, "_", ?Preceding_incarnation_member_TOL_ID)), ?unbound) AS ?preceding_incarnation_iri)

    # Preincarnations
    ### TODO - problem - what person is the preincarnation the preincarnation of?
    # BIND(IF(?Ordinal = 0, ?person_iri, ?unbound) AS ?preincarnation_iri)
    # BIND(IF(BOUND(?preincarnation)))
    
}
