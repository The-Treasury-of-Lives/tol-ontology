PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 

## COLUMNS 

# TOL Tradition ID
# BDRC T ID
# Wikidata ID
# Name
# Wylie Name
# Chinese
# Founder TOL ID (may be multiple)
# Parent TOL ID
# Description

# Run: 
# tarql -e utf-8 --dedup 1000 traditions.tq traditions-5-22-23.csv > tolTraditions.ttl


CONSTRUCT {

	<https://w3id.org/treasuryoflives/data/tolTraditions>
        a owl:Ontology ;
        skos:prefLabel "Traditions"@en ;
        skos:definition "Defines Tibetan traditions."@en ;
        skos:scopeNote "Traditions are instances of Tibetan religion that identifies as a cohesive group with distinct modes of ritual and learning, as well as associated institutions such as monasteries."@en ;
	    owl:imports <https://w3id.org/treasuryoflives/ontology/tolCoreX.x.x> ,
            <https://w3id.org/semanticarts/ontology/gistCore12.0.1> ;
	    owl:versionIRI <https://w3id.org/treasuryoflives/data/tolTraditionsX.x.x> ;
	.

    ?tradition_iri 
        a tolo:Tradition ;
        owl:sameAs 
            ?bdrc_iri , 
            ?wikidata_iri ;
        skos:prefLabel 
            ?name_en , 
            ?name_wylie , 
            ?name_chinese ;
        tolo:primaryName 
            ?name_en , 
            ?name_wylie , 
            ?name_chinese ;
        tolo:hasFounder ?founder_iri ;
        tolo:isDerivedFrom ?parent_tradition_iri ;
        # tolo:isAffiliatedWith ?monastery_iri ;
        .

    ?description_iri 
        a gist:FormattedContent ;
        skos:prefLabel ?description_label_en ;
        gist:containedText ?description_en ;
        gist:isAbout ?tradition_iri ;
        gist:isExpressedIn media-txt:html ;
        .
        
}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM IN (5, 12))

    # Tradition
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", ?Tradition_ID)) AS ?tradition_iri)

    # owl:sameAs
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_T_ID)) AS ?bdrc_iri)
    BIND(tarql:expandPrefixedName(CONCAT("wiki:", ?Wikidata_ID)) AS ?wikidata_iri) 

    # Names
    BIND(STRLANG(?Name, "en") AS ?name_en)
    BIND(STRLANG(?Wylie_Name, "bo-x-ewts") AS ?name_wylie)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?name_chinese)

    # Founder(s)
    OPTIONAL { ?founder apf:strSplit(?Founder_TOL_ID ",") }
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", REPLACE(?founder, "TOLP", ""))) AS ?founder_iri)

    # Parent tradition
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", REPLACE(?Parent_TOL_ID, "TOL", ""))) AS ?parent_tradition_iri)

    # Monastery affiliation
    # TODO - Should this come from here or from the person-tradition spreadsheet?
    # BIND(tarql:expandPrefixedName(CONCAT("told:_Institution_", REPLACE(?Tradition_ID__Comunity_, "TOL", ""))) AS ?monastery_iri)
   
    # Description
    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_Description_Tradition_", ?Tradition_ID)) AS ?description_iri)
    BIND(STRLANG((CONCAT(?Name, " Tradition")), "en") as ?description_label_en)
    BIND(STRLANG(?Description, "en") AS ?description_en)


}
