PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://ontologies.semanticarts.com/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tol: <https://ontologies.treasuryoflives.org/tol/>
PREFIX tolx: <https://taxonomies.treasuryoflives.org/>
PREFIX told: <https://data.treasuryoflives.org/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# ToL Tradition
# BDRC Tradition (not converted)
# BDRC T ID (not converted)
# BDRC ID 
# Convert (y/n)

CONSTRUCT {

    ?tradition a tol:Tradition ;
        gist:name ?name ;
        owl:sameAs ?bdrc_tradition ;
    .

}

FROM <file:traditions_tol_bdrc_mapping.csv>

WHERE {
    
    FILTER(?Convert = "y")

    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", REPLACE(?ToL_Tradition, ' ', '_'))) AS ?tradition)
    BIND(STRLANG(?ToL_Tradition, "bo-x-phon-en-m-tol") AS ?name)
    BIND(tarql:expandPrefixedName(?BDRC_ID) AS ?bdrc_tradition)

}
