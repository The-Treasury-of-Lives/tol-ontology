PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# Lineage ID
# Role
# BDRC ROLE ID (ignore - will get same as with roles)
# Lineage Place ID
# Lineage Name
# Wylie Lineage Name
# Lineage Tibetan Name
# Lineage Chinese Name
# Lineage Variant Names English
# Lineage Variant Names Tibetan
# Lineage Variant Names Wylie

# Run:
# tarql -e utf-8 --dedup 50000 monastery_lineages.tq monastery-lineages-8-1-24.csv > tolMonasteryLineages.ttl

CONSTRUCT {

    ?monastery_lineage_iri 
        a tolo:MonasteryLineage ;
        skos:prefLabel 
            ?pref_label_en ,
            ?pref_label_wylie ,
            ?pref_label_tibetan ,
            ?pref_label_chinese ;
        skos:altLabel 
            ?alt_label_en ,
            ?alt_label_wylie ,
            ?alt_label_tibetan ;
        tolo:primaryName
            ?pref_label_en ,
            ?pref_label_wylie ,
            ?pref_label_tibetan , 
            ?pref_label_chinese ;
        tolo:nameVariant
            ?alt_label_en ,
            ?alt_label_wylie ,
            ?alt_label_tibetan ;
        gist:isConnectedTo ?monastery_iri ;
        gist:isCategorizedBy ?role_iri ;
        .

}

WHERE {

    # Testing
    # FILTER($ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_MonasteryLineage_", ?Lineage_ID)) AS ?monastery_lineage_iri)

    # Role
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Role_", LCASE(REPLACE(?Role, " ", "_")))) AS ?role_iri)  

    # Monastery
    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", ?Lineage_Place_ID)) AS ?monastery_iri)

    # Names
    BIND(STRLANG(?Lineage_Name, "en") AS ?pref_label_en)
    BIND(STRLANG(?Wylie_Lineage_Name, "bo-x-ewts") AS ?pref_label_wylie)
    BIND(STRLANG(?Lineage_Tibetan_Name, "bo") AS ?pref_label_tibetan)
    BIND(STRLANG(?Lineage_Chinese_Name, "zh-Hani") AS ?pref_label_chinese)

    # # Variant names - no data yet
    # # English 
    # OPTIONAL {
    #     ?en_variant apf:strSplit(?Lineage_Variant_Names_English ";") 
    # }
    # BIND(STRLANG(?en_variant, "en") AS ?alt_label_en)

    # # Wylie
    # OPTIONAL {
    #     ?wylie_variant apf:strSplit(?Lineage_Variant_Names_Wylie ";") 
    # }
    # BIND(STRLANG(?wylie_variant, "bo-x-ewts") AS ?alt_label_wylie)

    # # Tibetan
    # OPTIONAL {
    #     ?tibetan_variant apf:strSplit(?Lineage_Variant_Names_Tibetan ";") 
    # }
    # BIND(STRLANG(?tibetan_variant, "bo") AS ?alt_label_tibetan)

}
