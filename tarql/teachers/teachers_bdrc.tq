PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# teacher_id
# teacher_tbrc_rid
# teacher_default_name
# teacher_default_name-_lang-encoding (ignore)
# teacher_has_bio
# student_id
# student_tbrc_rid
# student_default_name
# student_default_name-_lang-encoding (ignore)
# student_has_bio

# Run: 
# tarql --dedup 100000 teachers_bdrc.tq teacher-student-bdrc-11-05-23.csv > tolTeachersBdrc.ttl


CONSTRUCT {

    ?relationship_iri
        a tolo:TeacherStudentRelationship ;
        skos:prefLabel ?relationship_label_en ;
        gist:hasGiver ?teacher_iri ;
        gist:hasRecipient ?student_iri ;
        .

    ?teacher_iri
        a ?teacher_type ;
        skos:prefLabel ?teacher_name_wylie ;
        tolo:preferredName ?teacher_name_wylie ;
        owl:sameAs ?teacher_bdrc_iri ;
        .   

    ?student_iri
        a ?student_type ;
        skos:prefLabel ?student_name_wylie ;
        tolo:preferredName ?student_name_wylie ;
        owl:sameAs ?student_bdrc_iri ;
        .   

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM IN (31, 32))

    BIND(tarql:expandPrefixedName(CONCAT("told:_TeacherStudentRelationship_", ?teacher_id, "_", ?student_id)) AS ?relationship_iri)
    BIND(STRLANG(CONCAT("Teacher-Student Relationship between Person ", ?teacher_id, " and ", ?student_id), "en") AS ?relationship_label_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?teacher_id)) AS ?teacher_iri)
    # If no bio, the person entity has not been created in the persons conversion. Add type, name and owl:sameAs to BDRC person. Assume Wylie encoding of name, and ignore final parenthesized text (closing parenthesis is missing in one case).
    BIND(IF(?teacher_has_bio = "NO", REPLACE(?teacher_default_name, " \\(.*\\)?", ""), ?unbound) AS ?teacher_name_clean)
    BIND(IF(BOUND(?teacher_name_clean), STRLANG(?teacher_name_clean, "bo-x-ewts"), ?unbound) AS ?teacher_name_wylie)
    BIND(IF(BOUND(?teacher_name_wylie), tarql:expandPrefixedName(CONCAT("bdr:", ?teacher_tbrc_rid)), ?unbound) AS ?teacher_bdrc_iri)
    BIND(IF(BOUND(?teacher_name_wylie), tarql:expandPrefixedName("tolo:Person"), ?unbound) AS ?teacher_type)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?student_id)) AS ?student_iri)
    # If no bio, the person entity has not been created in the persons conversion. Add type, name and owl:sameAs to BDRC person. Assume Wylie encoding of name, and ignore final parenthesized text (closing parenthesis is missing in one case).
    BIND(IF(?student_has_bio = "NO", REPLACE(?student_default_name, " \\(.*\\)?", ""), ?unbound) AS ?student_name_clean)
    BIND(IF(BOUND(?student_name_clean), STRLANG(?student_name_clean, "bo-x-ewts"), ?unbound) AS ?bio_iristudent_name_wylie)
    BIND(IF(BOUND(?student_name_wylie), tarql:expandPrefixedName(CONCAT("bdr:", ?student_tbrc_rid)), ?unbound) AS ?student_bdrc_iri)
    BIND(IF(BOUND(?student_name_wylie), tarql:expandPrefixedName("tolo:Person"), ?unbound) AS ?student_type)

}
