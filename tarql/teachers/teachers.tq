PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# teacher_id
# tbrc_teacher_id
# teacher_default_name
# teacher_default_name-_lang-encoding
# teacher_has_bio
# student_id
# student_bdrc_id
# student_default_name
# student_default_name-_lang-encoding
# student_has_bio

# Run: 
# tarql --dedup 100000 teachers.tq teacher-student_2023-11-05.csv > tolTeachers.ttl


CONSTRUCT {

    ?relationship_iri
        a tolo:TeacherStudentRelationship ;
        skos:prefLabel ?relationship_label_en ;
        gist:hasGiver ?teacher_iri ;
        gist:hasRecipient ?student_iri ;
        .

    ?teacher_iri
        a ?teacher_type ;
        skos:prefLabel ?teacher_name ;
        tolo:preferredName ?teacher_name ;
        owl:sameAs ?teacher_bdrc_iri ;
        .   

    ?student_iri
        a ?student_type ;
        skos:prefLabel ?student_name ;
        tolo:preferredName ?student_name ;
        owl:sameAs ?student_bdrc_iri ;
        .   

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM IN (1, 464))

    BIND(tarql:expandPrefixedName(CONCAT("told:_TeacherStudentRelationship_", ?teacher_id, "_", ?student_id)) AS ?relationship_iri)
    BIND(STRLANG(CONCAT("Teacher-Student Relationship between Person ", ?teacher_id, " and ", ?student_id), "en") AS ?relationship_label_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?teacher_id)) AS ?teacher_iri)
    # If no bio, the person entity has not been created in the persons conversion. Add name and owl:sameAs to BDRC person.
    BIND(IF(?teacher_has_bio = "NO", STRLANG(?teacher_default_name, ?teacher_default_name__lang_encoding), ?unbound) AS ?teacher_name)
    BIND(IF(BOUND(?teacher_name), tarql:expandPrefixedName(CONCAT("bdr:", ?tbrc_teacher_id)), ?unbound) AS ?teacher_bdrc_iri)
    BIND(IF(BOUND(?teacher_name), tarql:expandPrefixedName("tolo:Person"), ?unbound) AS ?teacher_type)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?student_id)) AS ?student_iri)
    # If no bio, the person entity has not been created in the persons conversion. Add name and owl:sameAs to BDRC person.
    BIND(IF(?student_has_bio = "NO", STRLANG(?student_default_name, ?student_default_name__lang_encoding), ?unbound) AS ?student_name)
    BIND(IF(BOUND(?student_name), tarql:expandPrefixedName(CONCAT("bdr:", ?tbrc_student_id)), ?unbound) AS ?student_bdrc_iri)
    BIND(IF(BOUND(?student_name), tarql:expandPrefixedName("tolo:Person"), ?unbound) AS ?student_type)

}
