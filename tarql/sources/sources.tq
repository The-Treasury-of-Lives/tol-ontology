PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <http://id.loc.gov/vocabulary/relators/> 
PREFIX viaf: <https://viaf.org/viaf/>
PREFIX lccn: <https://id.loc.gov/resources/works/>
PREFIX olid: <https://openlibrary.org/works/> 


## COLUMNS 
# PERSON ID
# PLACE ID
# TOL Source ID
# Source Lang Tag
# Source
# Pages
# BDRC ID
# VIAF ID
# OCLC ID
# LCCN
# OLID ID
# Source Display Lang Tag
# Source Display

# Run: 
# tarql -e utf-8 --dedup 50000 sources.tq Sources-6-14-24.csv > tolSources.ttl


CONSTRUCT {

    ?subject_iri
        ?predicate_iri ?source_iri ;
        .

    ?source_iri
        a gist:ContentExpression ;
        skos:prefLabel ?source_label ;
        tolo:displayText ?display_text_iri ;
        owl:sameAs 
            ?bdrc_iri ,
            ?viaf_iri ,
            ?lccn_iri ,
            ?olid_iri ;
        
        gist:isIdentifiedBy 
            ?oclc_id_iri ;
        .

    ?display_text_iri
        a gist:FormattedContent ;
        gist:containedText ?display_text ;
        gist:isExpressedIn media-txt:html ;
        .

    ?worldcat_id_iri
        a gist:ID ;
        gist:uniqueText ?OCLC_ID ;
        gist:isCategorizedBy tolx:_IdType_worldcat ;
        .

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_ContentExpression_Source_", ?TOL_Source_ID)) AS ?source_iri)

    BIND(
        COALESCE(
            IF(BOUND(?PERSON_ID), tarql:expandPrefixedName(CONCAT("told:_Biography_", ?PERSON_ID)), ?unbound),
            IF(BOUND(?PLACE_ID), tarql:expandPrefixedName(CONCAT("told:_Place_", ?PLACE_ID)), ?unbound)
        )
        AS ?subject_iri 
    )

    BIND(
        COALESCE(
            IF(BOUND(?PERSON_ID), tarql:expandPrefixedName("tolo:hasSource"), ?unbound),
            IF(BOUND(?PLACE_ID), tarql:expandPrefixedName("rdfs:seeAlso"), ?unbound)
        )
        AS ?predicate_iri
    )

    BIND(STRLANG(?Source, ?Source_Lang_Tag) AS ?source_label) 

    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_Source_DisplayText_", ?TOL_Source_ID)) AS ?display_text_iri)
    BIND(STRLANG(?Source_Display, ?Source_Display_Lang_Tag) AS ?display_text)

    # owl:SameAs
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_ID)) AS ?bdrc_iri)
    BIND(tarql:expandPrefixedName(CONCAT("viaf:", ?VIAF_ID)) AS ?viaf_iri)
    BIND(tarql:expandPrefixedName(CONCAT("lccn:", ?LCCN)) AS ?lccn_iri)
    BIND(tarql:expandPrefixedName(CONCAT("olid:", ?OLID_ID)) AS ?olid_iri)

    # WorldCat 
    BIND(tarql:expandPrefixedName(CONCAT("told:_ID_worldcat_", ?OCLC_ID)) AS ?worldcat_id_iri)

}
