PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# COLUMNS
# person_image
# person_id
# image_file_name
# thumb_file_name
# har_number
# image_credit
# is_primary 
# create_user_id
# create_date
# last_modified_user_id
# last_modified_date
# caption

# Run: 
# tarql -e utf-8 person_images.tq person-image-3-18-24.csv > tolPersonImages.ttl

CONSTRUCT {

    ?image_iri
        a tolo:Image ;
        skos:prefLabel ?image_label_en ;
        gist:isAbout ?person_iri ;
        tolo:fileName ?image_file_name ;
        tolo:hasThumbnail ?thumbnail_iri ;
        rdfs:seeAlso ?har_url ;
        tolo:credit ?credit_en ;
        tolo:caption ?caption_en ;
        gist:isCategorizedBy ?rank_iri ;
        tolo:isCreatedBy ?created_by_iri ;
        tolo:createDateTime ?create_datetime ;
        tolo:isLastModifiedBy ?last_modified_by_iri ;
        tolo:lastModifiedDateTime ?last_modified_datetime ;
        .

    ?thumbnail_iri 
        a tolo:Image ;
        tolo:fileName ?thumb_file_name ;
        .

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Image_", ?person_image_id)) AS ?image_iri)
    BIND(STRLANG(CONCAT("Person ", ?person_id), "en") AS ?image_label_en)
    
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?person_id)) AS ?person_iri)
    
    BIND(tarql:expandPrefixedName(CONCAT("told:_Image_Thumbnail_", ?person_image_id)) AS ?thumbnail_iri)

    BIND(CONCAT("https://www.himalayanart.org/", ?har_number) AS ?har_url)

    BIND(IF(?image_credit != "NULL", STRLANG(?image_credit, "en"), ?unbound) AS ?credit_en)

    BIND(IF(?is_primary = "1", "primary", "secondary") AS ?rank_value)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Rank_", ?rank_value)) AS ?rank_iri)

    BIND(IF(?caption != "NULL", STRLANG(?caption, "en"), ?unbound) AS ?caption_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:_UserAccount_", ?create_user_id)) AS ?created_by_iri)

    BIND(REPLACE(?create_date, " .+$", "") AS ?create_day)
    BIND(REPLACE(?create_date, "^.+ ", "") AS ?create_time)
    BIND(IF(REGEX(?create_time, "^\\d:"), CONCAT("0", ?create_time), ?create_time) AS ?normalized_create_time)
    BIND(STRDT(CONCAT(?create_day, "T", ?normalized_create_time), xsd:dateTime) AS ?create_datetime)
    
    # If no modification date/user, use creation date/user.
    BIND(IF(?last_modified_user_id != "NULL", tarql:expandPrefixedName(CONCAT("told:_UserAccount_", ?last_modified_user_id)), ?created_by_iri) AS ?last_modified_by_iri)

    BIND(IF(?last_modified_date != "NULL", REPLACE(?last_modified_date, " .+$", ""), ?unbound) AS ?last_modified_day)
    BIND(IF(?last_modified_date != "NULL", REPLACE(?last_modified_date, "^.+ ", ""), ?unbound) AS ?last_modified_time)
    BIND(IF(REGEX(?last_modified_time, "^\\d:"), CONCAT("0", ?last_modified_time), ?last_modified_time) AS ?normalized_last_modified_time)
    BIND(IF(?last_modified_date != "NULL", STRDT(CONCAT(?last_modified_day, "T", ?normalized_last_modified_time), xsd:dateTime), ?create_datetime) AS ?last_modified_datetime)

}
