PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# community_id
# lat
# long
# icon
# map_priority_level_id
# map_display_name
# map_display_name_script

# Run:
# tarql -e utf-8 place_map_data.tq place-map-data-5-29-24.csv > tolPlaceMapData.ttl

CONSTRUCT {


    ?place_iri 
        tolo:isRenderedAt ?geo_point_iri ;
        tolo:displayLabel 
            ?display_label_en ,
            ?display_label_bo ;
        tolo:hasMappingPriority ?map_priority_iri ;   
    .

    ?geo_point_iri 
        a gist:GeoPoint ;
        gist:latitude ?latitude ;
        gist:longitude ?longitude ;

}

WHERE {

    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", ?community_id)) AS ?place_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_GeoPoint_", ?lat, "_", ?long)) AS ?geo_point_iri)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_MappingPriority_", ?map_priority_level_id)) AS ?map_priority_iri)
    BIND(STRDT(?lat, xsd:decimal) AS ?latitude)
    BIND(STRDT(?long, xsd:decimal) AS ?longitude)
    BIND(STRLANG(?map_display_name, "en") AS ?display_label_en)
    BIND(STRLANG(?map_display_name_script, "bo") AS ?display_label_bo)  

}
