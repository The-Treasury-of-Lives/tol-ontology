PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# TOL ID
# BDRC ID
# Wikidata ID
# Site (Place) Type
# Class
# Wylie
# Tibetan
# Chinese 
# Class - Landmark, Building, or Institution
# Definition

# Run:
# tarql -e utf-8 place_functions.tq place-types-7-14-24.csv > tolPlaceFunctions.ttl

CONSTRUCT {

    ?type_iri
        a tolo:PlaceFunction ;
        owl:sameAs 
            ?bdrc_iri ,
            ?wikidata_iri ;
        skos:prefLabel 
            ?label_en ,
            ?label_wylie ,
            ?label_tibetan ,
            ?label_chinese ;
        skos:definition ?definition_en ;
    .

}

WHERE {

    FILTER(?Class = "PlaceFunction")

    # Testing
    # FILTER(?ROWNUM = 2) 

    BIND(LCASE(REPLACE(?Site__Place__Type, " ", "_")) AS ?type)

    # Category
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_PlaceFunction_", ?type)) AS ?type_iri)

    # owl:sameAs
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_ID)) AS ?bdrc_iri)
    BIND(tarql:expandPrefixedName(CONCAT("wiki:", ?Wikidata_ID)) AS ?wikidata_iri)

    # Labels
    BIND(STRLANG(?Site__Place__Type, "en") AS ?label_en)
    BIND(STRLANG(?Wylie, "bo-x-ewts") AS ?label_wylie)
    BIND(STRLANG(?Tibetan, "bo") AS ?label_tibetan)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?label_chinese)

    BIND(STRLANG(?Definition, "en") AS ?definition_en)
}
