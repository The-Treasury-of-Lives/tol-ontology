PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# community_id
# community_name
# wylie_name
# tibetan
# chinese_name
# en_variants
# bo_x_ewts_variants
# zh_hani_variants
# sh_iast_variants
# sa_deva_variants
# ru_cyrl_variants
# mn_cyrl_variants
# mn_mong_variants

# Run:
# tarql -e utf-8 --dedup 50000 place_names.tq place-names-w-variants-5-29-24.csv > tolPlaceNames.ttl

CONSTRUCT {

    ?place_iri 
        skos:prefLabel 
            ?primary_name_en ,
            ?primary_name_wylie ,
            ?primary_name_tibetan ,
            ?primary_name_zh_hani 
            ;
        tolo:primaryName 
            ?primary_name_en ,
            ?primary_name_wylie ,
            ?primary_name_tibetan ,
            ?primary_name_zh_hani 
            ;    
        tolo:alternate_name ?alternate_name_en ,
            ?alternate_name_wylie ,
            ?alternate_name_zh_hani ,
            ?alternate_name_sa_deva ,
            ?alternate_name_sa_iast ,
            ?alternate_name_ru ,
            ?alternate_name_mn_cyrl ,
            ?alternate_name_mn_mong 
            ;
        .

}

WHERE {

    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", ?community_id)) AS ?place_iri)

    # Primary names
    BIND(STRLANG(?community_name, "en") AS ?primary_name_en)
    BIND(STRLANG(?wylie_name, "bo-x-ewts") AS ?primary_name_wylie)
    BIND(STRLANG(?chinese_name, "zh-Hani") AS ?primary_name_zh_hani)

    # Alternate names

    # English 
    OPTIONAL {
        ?en_variant apf:strSplit(?en_variants ";") 
    }
    BIND(STRLANG(?en_variant, "en") AS ?alternate_name_en)

    # Wylie transliteration
    OPTIONAL {
        ?wylie_variant apf:strSplit(?bo_x_ewts_variants ";") 
    }
    BIND(STRLANG(?wylie_variant, "bo-x-ewts") AS ?alternate_name_wylie)

    # Mandarin Chinese, simplified vs. traditional unspecified
    OPTIONAL {
        ?chinese_variant apf:strSplit(?zh_hani_variants ";") 
    }
    BIND(STRLANG(?chinese_variant, "zh-Hani") AS ?alternate_name_zh_hani)

    # Sanskrit Devangari
    OPTIONAL {
        ?sa_deva_variant apf:strSplit(?sa_deva_variants ";") 
    }
    BIND(STRLANG(?sa_deva_variants, "sa-Deva") AS ?alternate_name_sa_deva)

    # Sanskrit IAST transliteration
    OPTIONAL {
        ?sa_iast_variant apf:strSplit(?sa_iast_variants ";") 
    }
    BIND(STRLANG(?sa_iast_variants, "sa-x-iast") AS ?alternate_name_sa_iast)

    # Russian 
    OPTIONAL {
        ?ru_variant apf:strSplit(?ru_variants ";") 
    }
    BIND(STRLANG(?ru_variant, "ru") AS ?alternate_name_ru)

    # Classical Mongolian in Cyrillic script
    OPTIONAL {
        ?mn_cyrl_variant apf:strSplit(?mn_cyrl_variants ";") 
    }
    BIND(STRLANG(?mn_cyrl_variant, "mn-Cyrl") AS ?alternate_name_mn_cyrl)

    # Classic Mongolian in Mongolian script
    OPTIONAL {
        ?mn_mong_variant apf:strSplit(?mn_mong_variants ";") 
    }
    BIND(STRLANG(?mn_mong_variant, "mn-Mong") AS ?alternate_name_mn_mong)
}
