PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# community_id
# tbrc_id (ignore)
# community_name 
# founded_date
# founded_date_approximate
# founded_date_alternate
# founded_century_id
# existed_by_century_id


# Run:
# tarql -e utf-8 place_founding_dates.tq place-founding-dates-6-9-24.csv > tolPlaceFoundingDates.ttl

CONSTRUCT {

    ?founding_iri 
        # Type assertion comes from place_founders.tq
        skos:prefLabel ?founding_label_en ;
        tolo:actualYear ?actual_datetime ;
        tolo:approximateYear ?approximate_datetime;
        tolo:alternateYear 
            ?alternate_datetime_1 ,
            ?alternate_datetime_2 ;
        tolo:occursDuring ?founding_time_period_iri ;
        tolo:occursBy ?occurred_by_time_period_iri ;
        .

}

WHERE {

    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Founding_", ?community_id)) AS ?founding_iri)

    BIND(STRLANG(CONCAT(?community_name, " Founding"), "en") AS ?founding_label_en)

    # To create an xsd:dateTime from a year string
    BIND("-01-01T00:00:00" AS ?date_time_completion)

    BIND(
        COALESCE(
            IF(REGEX(?founded_date, "^\\d{4}$"), ?founded_date, ?unbound), 
            # Add initial 0 to 3-digit year to create a well-formed year
            IF(REGEX(?founded_date, "^\\d{3}$"), CONCAT("0", ?founded_date), ?unbound)
        ) 
        AS ?founded_year
    )

    BIND(STRDT(CONCAT(?founded_year, ?date_time_completion), xsd:dateTime) AS ?founded_datetime)

    BIND(STRDT(CONCAT(?founded_date_alternate, ?date_time_completion), xsd:dateTime) AS ?alternate_datetime_1)

    # Actual vs approximate vs alternate founding year:

    # If no alternate and date is not approximate, it is the actual date.
    BIND(IF(!BOUND(?founded_date_alternate) && ?founded_date_approximate = "0", ?founded_datetime, ?unbound) AS ?actual_datetime)

    # If approximate, founded date is the approximate date.
    # In some cases approximate = 1 co-occurs with founded_century_id or existed_by_century_id. The
    # approximate value is ignored in this case, because it servers only to make actual years approximate. Century-based founding is already approximate by definition.
    BIND(IF(?founded_date_approximate = "1", ?founded_datetime, ?unbound) AS ?approximate_datetime)

    # If there's an alternate date, the founded date is an alternate (neither has priority).
    BIND(IF(BOUND(?founded_date_alternate), ?founded_datetime, ?unbound) AS ?alternate_datetime_2)

    # Founding century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?founded_century_id)) AS ?founding_time_period_iri) 

    # Existed by century
    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?existed_by_century_id)) AS ?occurred_by_time_period_iri) 
    
}
