PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# TOL ID
# BDRC ID
# Wikidata ID
# Site (Place) Type
# Wylie
# Tibetan
# Chinese 
# Class - Landmark, Building, or Institution

# Note that Institutions are not places, but are included here because the relational database and thus the export combines them with places.

# Run:
# tarql place_types.tq place-types-5-16-23.csv > tolPlaceTypes.ttl

CONSTRUCT {

    ?type_iri
        a ?class_iri ;
        owl:sameAs 
            ?bdrc_iri ,
            ?wikidata_iri ;
        skos:prefLabel 
            ?label_en ,
            ?label_wylie ,
            ?label_tibetan ,
            ?label_chinese ;
        skos:definition ?definition_en ;
    .

}

WHERE {

    # Testing
    # FILTER(?ROWNUM IN (1, 7, 13)) 

    BIND(LCASE(REPLACE(?Site__Place__Type, " ", "_")) AS ?type)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_", ?Class, "Type_", ?type)) AS ?type_iri)

    # owl:sameAs
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_ID)) AS ?bdrc_iri)
    BIND(tarql:expandPrefixedName(CONCAT("wiki:", ?Wikidata_ID)) AS ?wikidata_iri)

    # Labels
    BIND(STRLANG(?Site__Place__Type, "en") AS ?label_en)
    BIND(STRLANG(?Wylie, "bo-x-ewts") AS ?label_wylie)
    BIND(STRLANG(?Tibetan, "bo") AS ?label_tibetan)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?label_chinese)

    BIND(IF(?Class = "Institution", 
            tarql:expandPrefixedName(CONCAT("tolo:", "InstitutionType")), 
            tarql:expandPrefixedName(CONCAT("gist:", ?Class, "Type"))       
        ) AS ?class_iri)


    BIND(STRLANG(?Definition, "en") AS ?definition_en)
}
