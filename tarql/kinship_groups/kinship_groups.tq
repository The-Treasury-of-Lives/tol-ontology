PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://ontologies.semanticarts.com/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tolo: <https://ontologies.treasuryoflives.org/tol/>
PREFIX tolx: <https://taxonomies.treasuryoflives.org/>
PREFIX told: <https://data.treasuryoflives.org/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 

## COLUMNS
# Name
# TOL ID
# Family Type Name
# Wylie Name
# Chinese
# BDRC ID
# Members

# Run: 
# tarql --dedup 1000 kinship_groups.tq > kinship_groups.ttl

CONSTRUCT {

    ?kinship_group_iri 
        a tolo:KinshipGroup ; 
        owl:sameAs ?bdrc_id ;
        tolo:preferredName ?preferred_name_tol , 
            ?preferred_name_wylie ,
            ?preferred_name_chinese ;
        gist:isCategorizedBy ?kinship_group_type_iri ;
        gist:hasMember ?person_iri ;
    .

}

FROM <file:kinship_groups_2023-05-25.csv>

WHERE {
    
    BIND(REPLACE(?TOL_ID, "TOL", "") AS ?tol_id)
    BIND(tarql:expandPrefixedName(CONCAT("told:_KinshipGroup_", ?tol_id)) AS ?kinship_group_iri)

    # Equivalent BDRC resource
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_ID)) AS ?bdrc_id)

    # Names
    BIND(STRLANG(?Name, "en") AS ?preferred_name_tol)
    BIND(STRLANG(?Wylie_Name, "bo-x-ewts") AS ?preferred_name_wylie)
    BIND(STRLANG(?Chinese, "zh-Hani") AS ?preferred_name_chinese)

    # Class and category
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_KinshipGroupType_", REPLACE(?Family_Type_Name, ' ', '_'))) AS ?kinship_group_type_iri)

    # Group members
    OPTIONAL { ?person apf:strSplit(?Members ";") }
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?person)) AS ?person_iri)

}
