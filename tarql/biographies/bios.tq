PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# biography_id
# person_id
# summary
# body_text
# author_id
# display_order - not sure what to do with this

# Run: 
# tarql --dedup 100000 bios.tq bio-full-text_2023-07-12.csv > tolBios.ttl


CONSTRUCT {

    ?bio_iri
        a tolo:HtmlBiography ;
        skos:prefLabel ?bio_label_en ;
        gist:isAbout ?person_iri ;
        gist:containedText ?bio_text_en ;
	    gist:isExpressedIn media-txt:html ;
        bf:summary ?summary_iri ;
        bf:contribution ?contribution_iri ;
        .

    ?summary_iri
        a bf:Summary ;
        skos:prefLabel ?summary_label_en ;
        gist:isExpressedIn media-txt:html ;
        gist:containedText ?summary_text_en ;
        .

    ?contribution_iri 
        a bf:Contribution ;
        skos:prefLabel ?contribution_label_en ;
        bf:role relators:aut ;
        bf:agent ?author_iri ;
        .      
}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1154)

    BIND(tarql:expandPrefixedName(CONCAT("told:_HtmlBiography_", ?biography_id)) AS ?bio_iri)
    BIND(STRLANG(?body_text, "en") AS ?bio_text_en)
    BIND(STRLANG(CONCAT("Biography of Person ", ?person_id), "en") AS ?bio_label_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?person_id)) AS ?person_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Summary_HtmlBiography_", ?biography_id)) AS ?summary_iri)
    BIND(STRLANG(?summary, "en") AS ?summary_text_en)
    BIND(STRLANG(CONCAT("Summary of Biography of Person ", ?person_id), "en") AS ?summary_label_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:Contribution_HtmlBiography_", ?person_id, "_Author_", ?author_id)) AS ?contribution_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Author_", ?author_id)) AS ?author_iri)
    BIND(STRLANG(CONCAT("Authorship of Biography of Person ", ?person_id, " by Author ", ?author_id), "en") AS ?contribution_label_en)

    # Display order - awaiting input
}
