PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# map_region_id
# bdrc_id
# label
# wylie
# tibetan
# chinese
# lat
# long
# zoom_min
# zoom_max
# type
# zoom_level_on_click
# map_priority_level_id

# Run:
# tarql -e utf-8 region_map_data.tq region-map-data-5-20-24.csv > tolRegionMapData.ttl

CONSTRUCT {

    ?region_iri 
        a gist:GeoRegion ;
        tolo:isRenderedAt ?geo_point_iri ;
        skos:prefLabel 
            ?label_en ,
            ?label_wylie ,
            ?label_tibetan ,
            ?label_chinese             
            ;
        tolo:displayLabel 
            ?label_en ,
            ?label_wylie ,
            ?label_tibetan ,
            ?label_chinese             
            ;
        owl:sameAs ?bdrc_iri ;
        tolo:hasMappingPriority ?map_priority_iri ;  
        tolo:minZoomLevel ?min_zoom_level ;
        tolo:maxZoomLevel ?max_zoom_level ; 
        tolo:zoomLevelOnClick ?on_click_zoom_level ; 
        gist:isCategorizedBy    
            ?region_type_iri ;
    .

    ?geo_point_iri 
        a gist:GeoPoint ;
        gist:latitude ?latitude ;
        gist:longitude ?longitude ;
    .

}

WHERE {

    # Testing
    #FILTER(?ROWNUM = 22)

    BIND(tarql:expandPrefixedName(CONCAT("told:_GeoRegion_", ?map_region_id)) AS ?region_iri)
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?bdrc_id)) AS ?bdrc_iri)

    BIND(STRLANG(?label, "en") AS ?label_en)
    BIND(STRLANG(?tibetan, "bo") AS ?label_tibetan)  
    BIND(STRLANG(?wylie, "bo-x-ewts") AS ?label_wylie)
    BIND(STRLANG(?chinese, "zh-Hani") AS ?label_chinese) 

    BIND(tarql:expandPrefixedName(CONCAT("told:_GeoPoint_", ?lat, "_", ?long)) AS ?geo_point_iri)
    BIND(STRDT(?lat, xsd:decimal) AS ?latitude)
    BIND(STRDT(?long, xsd:decimal) AS ?longitude)

    BIND(tarql:expandPrefixedName(CONCAT("tolx:_MappingPriority_", ?map_priority_level_id)) AS ?map_priority_iri)
    BIND(STRDT(?zoom_min, xsd:integer) AS ?min_zoom_level)
    BIND(STRDT(?zoom_max, xsd:integer) AS ?max_zoom_level)
    BIND(STRDT(?zoom_level_on_click, xsd:integer) AS ?on_click_zoom_level)

    BIND(COALESCE(
        IF(?type = "city", "county_seat", ?unbound),
        IF(?type = "region", "folk_region", ?unbound),
        ?unbound
        ) AS ?region_type
    )
    BIND(IF(BOUND(?region_type), tarql:expandPrefixedName(CONCAT("tolx:_RegionType_", ?region_type)), ?unbound) AS ?region_type_iri)

}
