PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

## Columns
# TOL ID
# BDRC ID
# Wikidata ID
# Site (Place) Type
# Class
# Wylie
# Wylie Variant (semicolon + space-delimited list)
# Tibetan
# Tibetan Variant (semicolon + space-delimited list)
# Chinese
# Class (Landmark, Building, or Institution) - ignore: these are all typed as gist:Place
# Definition

# Run:
# tarql -e utf-8 --dedup 10000 place_types.tq place-types-10-21-24.csv > tolPlaceTypes.ttl

CONSTRUCT {

    <https://w3id.org/treasuryoflives/taxonomy/tolPlaceTypes>
        a owl:Ontology ;
        owl:imports <https://w3id.org/treasuryoflives/ontology/tolCoreX.x.x> ;
        owl:versionIRI <https://w3id.org/treasuryoflives/taxonomy/tolPlaceTypesX.x.x> ;
        skos:definition "Defines types of places. These may be functional types, such as monastery, school, and hospital, or types of natural features, such as cave and mountain. Some places have more than one category - e.g., a cave can also be a hermitage."@en ;
        skos:editorialNote "Generated via TARQL."@en ;
        skos:prefLabel "Place Types"@en ;
        gist:license "https://creativecommons.org/licenses/by/4.0/"^^xsd:anyURI ;
	.

    ?type_iri
        a tolo:PlaceType ;
        owl:sameAs
            ?bdrc_iri ,
            ?wikidata_iri ;
        skos:prefLabel
            ?pref_label_en ,
            ?pref_label_wylie ,
            ?pref_label_tibetan ,
            ?pref_label_chinese ;
        skos:altLabel
            ?alt_label_wylie ,
            ?alt_label_tibetan ;
        skos:definition ?definition_en ;
    .

}

WHERE {

    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(LCASE(REPLACE(?Site__Place__Type, " ", "_")) AS ?type)

    # Category
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_PlaceType_", ?type)) AS ?type_iri)

    # owl:sameAs
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?BDRC_ID)) AS ?bdrc_iri)
    BIND(tarql:expandPrefixedName(CONCAT("wiki:", ?Wikidata_ID)) AS ?wikidata_iri)

    # Labels

    # English
    BIND(STRLANG(?Site__Place__Type, "en") AS ?pref_label_en)

    # Wylie
    BIND(STRLANG(?Wylie, "bo-x-ewts") AS ?pref_label_wylie)
    OPTIONAL {
        ?wylie_variant apf:strSplit(?Wylie_Variant "; ")
    }
    BIND(STRLANG(?wylie_variant, "bo-x-ewts") AS ?alt_label_wylie)

    # Tibetan
    BIND(STRLANG(?Tibetan, "bo") AS ?pref_label_tibetan)
    OPTIONAL {
        ?tibetan_variant apf:strSplit(?Tibetan_Variant "; ")
    }
    BIND(STRLANG(?tibetan_variant, "bo") AS ?alt_label_tibetan)

    BIND(STRLANG(?Chinese, "zh-Hani") AS ?pref_label_chinese)

    BIND(STRLANG(?Definition, "en") AS ?definition_en)
}
