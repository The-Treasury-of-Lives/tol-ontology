PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://ontologies.semanticarts.com/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX tol: <https://ontologies.treasuryoflives.org/tol/>
PREFIX tolx: <https://taxonomies.treasuryoflives.org/>
PREFIX told: <https://data.treasuryoflives.org/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>

# TOL ID
# TBRC RID
# Primary title / epithet
# Primary name
# Wylie title / epithet
# Wylie name
# Birth year
# Birth year approximate?
# Birth year alternate
# Death year
# Death year approximate?
# Death year alternate
# Gender
# HAR URL
# Death century
# Historical period
# Name displayed as
# Name variants
# Birth century

# TODO!! 
# Name variants
# actual/probable/possible birth and death years
# parse multiple alternate birth/death years

CONSTRUCT {
    ?personResource a tol:Person ;
        owl:sameAs ?bdrcResource ;
        owl:sameAs ?wikidataResource ;

        tol:titlePrimary ?titlePrimary ;
        tol:namePrimary ?namePrimary ;
        tol:titleVariant ?titleWylie ;
        tol:nameVariant ?nameWylie ;

        tol:actualBirthYear ?actualBirthYear  ;
        tol:probableBirthYear ?probableBirthYear ;
        tol:possibleBirthYear ?possibleBirthYear ;
        tol:approximateBirthYear ?approximateBirthYear ;
        tol:birthCentury ?birthCentury ;  

        tol:actualDeathYear ?actualDeathYear ;
        tol:probableDeathYear ?probableDeathYear ;
        tol:possibleDeathYear ?possibleDeathYear ;
        tol:approximateDeathYear ?approximateDeathYear ;
        tol:deathCentury ?deathCentury ;

        bdo:personGender ?gender ;
        tol:harUrl ?HAR_URL ;
    .
}

# FROM <file:persons_2021-08-09.csv>

WHERE {

    # owl:sameAs
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?TOL_ID)) AS ?personResource)
    BIND(tarql:expandPrefixedName(CONCAT("bdr:", ?TBRC_RID)) AS ?bdrcResource)
    BIND(tarql:expandPrefixedName(CONCAT("wiki:", ?Wikidata_ID)) AS ?wikidataResource) 

    # TOL name and title
    BIND(STRLANG(?Primary_name, "bo-x-phon-en-m-tol") AS ?namePrimary)
    BIND(STRLANG(?Primary_title__epithet, "bo-x-phon-en-m-tol") AS ?titlePrimary)

    # Wylie name and title
    BIND(STRLANG(?Wylie_name, "bo-x-phon-ewts") AS ?nameWylie)
    BIND(STRLANG(?Wylie_title, "bo-x-phon-ewts") AS ?titleWylie)

    BIND("-01-01T00:00:00" AS ?dateTimeRemainder)

    # Birth year: actual, probable, possible, approximate
    BIND(
        IF(REGEX(?Birth_year, "^\\d{3}$"), 
            CONCAT("0", ?Birth_year), 
                # Two-digit birth year (11, 13) is always accompanied by a century, so don't generate a birth year. There is no data of this type for death years.
                IF(REGEX(?Birth_year, "^\\d{2}$"), ?blank, ?Birth_year)) AS ?fullBirthYear)
    BIND(
        IF(BOUND(?fullBirthYear), 
            STRDT(CONCAT(?fullBirthYear, ?dateTimeRemainder), xsd:dateTime), ?blank) AS ?birthYear)
    BIND(IF(BOUND(?birthYear) && ?Birth_year_approximate_ = "1", ?birthYear, ?blank) AS ?approximateBirthYear) 
    #### This is where we need to select actual vs probable:
    BIND(IF(BOUND(?birthYear) && !BOUND(?approximateBirthYear), ?birthYear, ?blank) AS ?actualBirthYear)

    # Death year: actual, probable, possible, approximate
    BIND(IF(REGEX(?Death_year, "^\\d{3}$"), CONCAT("0", ?Death_year), ?Death_year) AS ?fullDeathYear)
    BIND(STRDT(CONCAT(?fullDeathYear, ?dateTimeRemainder), xsd:dateTime) AS ?deathYear)
    BIND(IF(?Death_year_approximate_ = "1", ?deathYear, ?blank) AS ?approximateDeathYear) 
    #### This is where we need to select actual vs probable:
    BIND(IF(!BOUND(?approximateDeathYear), ?deathYear, ?blank) AS ?actualDeathYear)

    # Proposed birth years
    OPTIONAL {      
       ?alternateBirthYear apf:strSplit(?Birth_year_alternate ",")
    }
    BIND(IF(BOUND(?alternateBirthYear), STRDT(CONCAT(?alternateBirthYear, ?dateTimeRemainder), xsd:dateTime), ?blank) AS ?possibleBirthYear)

    # Proposed death years
    OPTIONAL {      
       ?alternateDeathYear apf:strSplit(?Death_year_alternate ",")
    }
    BIND(IF(BOUND(?alternateDeathYear), STRDT(CONCAT(?alternateDeathYear, ?dateTimeRemainder), xsd:dateTime), ?blank) AS ?possibleDeathYear)

    # Birth and death centuries
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Century_", REPLACE(REPLACE(?Birth_century, " ", "_"), "[()]", ""))) AS ?birthCentury) 
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_Century_", REPLACE(REPLACE(?Death_century, " ", "_"), "[()]", ""))) AS ?deathCentury) 

    # Gender
    BIND(IF(?Gender = "M", tarql:expandPrefixedName("bdr:GenderMale"), IF(?Gender = "F", tarql:expandPrefixedName("bdr:GenderFemale"), ?blank)) AS ?gender) 

    # Name variants
    OPTIONAL {

    }

}
