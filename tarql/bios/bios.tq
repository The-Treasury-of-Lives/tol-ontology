PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/> 
PREFIX relators: <ttp://id.loc.gov/vocabulary/relators/> 

## COLUMNS 
# biography_id - temporarily using to mint bio iri in order to link to the author display order data. Would prefer to use person id instead - TBD.
# person_id
# summary (HTML text)
# body_text
# sources (HTML text)
# peer_reviewed (binary)
# published_display_date
# updated_display_date
# published_default_name
# relative_statement (binary)
# student_statement (binary)

# Run: 
# tarql --dedup 100000 bios.tq bio-4-22-24.csv > tolBios.ttl


CONSTRUCT {

    ?bio_iri
        a tolo:Biography ;
        skos:prefLabel ?bio_label_en ;
        gist:isAbout ?person_iri ;
        gist:containedText ?bio_text_en ;
	    gist:isExpressedIn media-txt:html ;
        gist:isCategorizedBy   
            ?peer_reviewed_category ,
            ?relative_statement_category ,
            ?student_statement_category ;
        tolo:publicationDisplayDate ?published_display_date ;
        tolo:updateDisplayDate ?updated_display_date ;
        .

    ?summary_iri
        a tolo:Summary ;
        skos:prefLabel ?summary_label_en ;
        gist:isExpressedIn media-txt:html ;
        gist:containedText ?summary_text_en ;
        gist:isAbout ?bio_iri ;
        .

    ?source_statement_iri
        a tolo:SourceStatement ;
        skos:prefLabel ?source_statement_label_en ;
        gist:isExpressedIn media-txt:html ;
        gist:containedText ?source_statement_en ;
        gist:isAbout ?bio_iri ;
        .  

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Biography_", ?biography_id)) AS ?bio_iri)
    BIND(STRLANG(?body_text, "en") AS ?bio_text_en)
    BIND(STRLANG(CONCAT("Biography of Person ", ?person_id), "en") AS ?bio_label_en)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Person_", ?person_id)) AS ?person_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Summary_Biography_", ?biography_id)) AS ?summary_iri)
    BIND(STRLANG(?summary, "en") AS ?summary_text_en)
    BIND(STRLANG(CONCAT("Summary of Biography of Person ", ?person_id), "en") AS ?summary_label_en)

    BIND(tarql:expandPrefixedName(CONCAT("told:_SourceStatement_Biography_", ?biography_id)) AS ?source_statement_iri)
    BIND(STRLANG(?sources, "en") AS ?source_statement_en)
    BIND(STRLANG(CONCAT("Sources for Biography of Person ", ?person_id), "en") AS ?source_statement_label_en)

    BIND(IF(?relative_statement = "1", "true", "false") AS ?relative_statement_value)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_RelativeStatement_", ?relative_statement_value)) AS ?relative_statement_category)

    BIND(IF(?student_statement = "1", "true", "false") AS ?student_statement_value)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_StudentStatement_", ?student_statement_value)) AS ?student_statement_category)

    BIND(IF(?peer_reviewed = "1", "true", "false") AS ?peer_reviewed_value)
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_PeerReviewed_", ?peer_reviewed_value)) AS ?peer_reviewed_category)
   
}
